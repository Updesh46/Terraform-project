data "azurerm_subnet" "subnet_id" {
  for_each = var.vm

  name                 = each.value.subnet
  virtual_network_name = each.value.vnet_name
  resource_group_name  = each.value.rg_name
}


data "azurerm_public_ip" "public_ip_id" {
  for_each = {
    for k, v in var.vm : k => v
    if lookup(v, "pip_name", null) != null
  }

  name                = each.value.pip_name
  resource_group_name = each.value.rg_name
}


resource "azurerm_network_interface" "nic" {
  for_each = var.vm

  name                = each.value.nic_name
  location            = each.value.location
  resource_group_name = each.value.rg_name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = data.azurerm_subnet.subnet_id[each.key].id
    private_ip_address_allocation = "Dynamic"

    public_ip_address_id = lookup(
      data.azurerm_public_ip.public_ip_id,
      each.key,
      null
    ) != null ? data.azurerm_public_ip.public_ip_id[each.key].id : null
  }
}



resource "azurerm_linux_virtual_machine" "vm" {
  for_each                        = var.vm
  name                            = each.value.vm_name
  location                        = each.value.location
  resource_group_name             = each.value.rg_name
  size                            = "Standard_B1s"
  admin_username                  = each.value.admin_username
  admin_password                  = each.value.admin_password
  disable_password_authentication = false
  network_interface_ids           = [azurerm_network_interface.nic[each.key].id]

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts-gen2"
    version   = "latest"
  }

}